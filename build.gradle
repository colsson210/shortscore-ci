ext {
    dockerImageName = "shortscoreci"
    version = "1"
    dockerTagName = "$dockerImageName:$version"
    dockerContainerName = "${dockerImageName}Container"
    jenkinsPort = 8080
    jenkinsHost = 'localhost'
    jenkinsURL = project.hasProperty('jenkinsURL') ? jenkinsURL : "http://$jenkinsHost:$jenkinsPort"
    dockerDetached = project.hasProperty('dockerDetached') ? dockerDetached : 'false'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

task dockerRun(type: Exec) {
    group 'Docker'
    description "Run the jenkins docker container locally"
    commandLine 'docker'
    args = ['run', dockerDetached == 'true' ? '--detach' : '--rm', '--name', dockerContainerName, '--publish', "$jenkinsPort:$jenkinsPort", '--publish', '50000:50000', '--volume', '/var/run/docker.sock:/var/run/docker.sock', '--volume', '/usr/bin/docker:/usr/bin/docker', '--volume', '/var/run:/var/run:rw', '--volume', '/var/jenkins_home', dockerTagName]
    doLast {
        try {
            if(dockerDetached == 'true'){
                waitForHTTPResponse(jenkinsURL, 200)
                println "Jenkins server started at $jenkinsURL"
            }
        } catch (e) {
            throw new GradleException("Could not connect to $jenkinsURL", e)
        }
    }
}

@groovy.transform.TimedInterrupt(120L)
def void waitForHTTPResponse(String url, int responseCode) {
    println "Waiting for HTTP response $responseCode for '$url'"
    boolean isConnected = false
    while (!isConnected) {
        try {
            isConnected = url.toURL().openConnection().responseCode == responseCode
        } catch (any) {
        }
        Thread.sleep(500)
    }
}

task dockerStop(type: Exec) {
    group 'Docker'
    description 'Stop the jenkins docker container'
    commandLine 'docker'
    args = ['rm', '--force', '--volumes', dockerContainerName]
}

task dockerStatus(type: Exec) {
    group 'Docker'
    description 'Display the process status of the jenkins docker container'
    commandLine 'docker'
    args = ['ps', '-all', '--filter', "name=$dockerContainerName"]
}

task dockerBuild(type: Exec) {
    group 'Docker'
    description "Build the jenkins docker image"
    commandLine 'docker'
    args = ['build', '--tag', dockerTagName, "."]
}

task dockerPush(type: Exec) {
    group 'Docker'
    description 'Push the jenkins docker image'
    commandLine 'docker'
    args = ['push', dockerImageName]
}
